
plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.4.1'
    id 'idea'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}
scmVersion {
    tag {
        prefix = ''
        versionSeparator = ''
    }
    ignoreUncommittedChanges = false
}
def base64Decode = { String prop ->
    project.findProperty(prop) ?
    new String(Base64.getDecoder().decode(project.findProperty(prop).toString())).trim() :
    null
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

allprojects {
    group = "org.rundeck.cli-toolbelt"
    project.version = scmVersion.version
}
subprojects{
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    publishing {
        publications {
            "${project.name}lib"(MavenPublication) {
                from components.java
                version = project.version
                pom {
                    name = "Rundeck CLI Toolbelt - ${project.name}"
                    description = "CLI utility - ${project.name}"
                    url = 'https://github.com/rundeck/cli-toolbelt'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gschueler'
                            name = 'Greg Schueler'
                            email = 'greg@rundeck.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/rundeck/cli-toolbelt.git'
                        developerConnection = 'scm:git:ssh://github.com/rundeck/cli-toolbelt.git'
                        url = 'https://github.com/rundeck/cli-toolbelt'
                    }
                }
            }
        }
    }

    if(project.hasProperty('signingKey') && project.hasProperty('signingPassword')) {
        signing {
            useInMemoryPgpKeys(base64Decode("signingKey"), project.signingPassword)
            sign(publishing.publications)
        }
    }
}


repositories {
    mavenCentral()
}

